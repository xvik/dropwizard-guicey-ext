plugins {
    id 'ru.vyarus.java-lib' version '1.1.2' apply false
    id 'ru.vyarus.github-info' version '1.1.0' apply false
    id 'ru.vyarus.quality' version '3.1.1' apply false
    id 'com.jfrog.bintray' version '1.8.4' apply false
    id "io.spring.dependency-management" version "1.0.6.RELEASE" apply false

    id 'base'
    id 'jacoco'
    id 'net.researchgate.release' version '2.7.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.github.ben-manes.versions' version '0.20.0'
}

wrapper {
    gradleVersion = '4.8.1'
}

description = 'Dropwizard-guicey extensions'

allprojects {
    apply plugin: 'project-report'
    apply plugin: 'com.jfrog.bintray'

    repositories { mavenCentral(); mavenLocal(); maven { url 'https://jitpack.io' } }

    bintray {
        user = findProperty('bintrayUser')
        key = findProperty('bintrayKey')
        pkg {
            repo = 'xvik'
            name = rootProject.name
            desc = rootProject.description
            labels = ['dropwizard', 'guice']
            publicDownloadNumbers = true
        }
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'ru.vyarus.java-lib'
    apply plugin: 'ru.vyarus.quality'
    apply plugin: 'ru.vyarus.github-info'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    ext {
        guicey = 'dw-1.3-SNAPSHOT'
        guiceExtAnn = '1.3.0'
    }

    dependencyManagement {
        imports {
            mavenBom "ru.vyarus:dropwizard-guicey:$guicey"
        }
        dependencies {
            dependency "ru.vyarus:dropwizard-guicey:$guicey"
            dependency "ru.vyarus:guice-ext-annotations:$guiceExtAnn"
        }
    }
    dependencies {
        provided 'com.github.spotbugs:spotbugs-annotations:3.1.2'

        compile "ru.vyarus:dropwizard-guicey"

        testCompile "org.spockframework:spock-core"
        testCompile "io.dropwizard:dropwizard-testing"
        testCompile 'com.github.stefanbirkner:system-rules'
    }

    group = 'ru.vyarus.guicey'

    github {
        user = 'xvik'
        repository = rootProject.name
        license = 'MIT'
        licenseUrl = github.rawFileUrl('LICENSE');
        changelogFile = 'CHANGELOG.md'
    }

    pom {
        developers {
            developer {
                id 'xvik'
                name 'Vyacheslav Rusakov'
                email 'vyarus@gmail.com'
            }
        }
    }

    bintray {
        publications = ['maven']
    }

    test {
        testLogging {
            events 'skipped', 'failed', 'standard_error'
            exceptionFormat 'full'
        }
        maxHeapSize = '512m'
    }
    jacocoTestReport.reports.xml.enabled = true

    dependencyUpdates.revision = 'release'

    task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
            'Generates dependencies tree report and opens it in browser') {
        doLast {
            java.awt.Desktop.getDesktop().open(file("build/reports/project/dependencies/root.${project.name}.html"))
        }
    }
}

/*
 Each module published separately: subprojects simply upload files to bintray and root project will
 publish/generate signatures/sync with maven central.
 Note: plugin will find bintray tasks in submodules and depend on it so order will be always correct.
 */
bintray {
    publish = true
    pkg {
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

// by default, task depends on subproject tasks directly, which breaks dependencies graph
bintrayUpload.dependsOn subprojects.collect { ":$it.name:bintrayUpload" }

// dependency on all subprojects required for release validation
check.dependsOn subprojects.check

// release manages only root project (subprojects will be checked and released implicitly)
afterReleaseBuild {
    dependsOn = ['bintrayUpload']
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

release.git.requireBranch = 'guicey-4'

task testReport(type: TestReport, description: 'Generates an aggregate test report') {
    destinationDir = file("${buildDir}/reports/tests")
    reportOn subprojects.test
}

task jacocoMerge(type: JacocoMerge, description: 'Merge coverage results for aggregated report generation') {
    executionData = files(subprojects.collect {it.file('build/jacoco/test.exec')})
    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}

task jacocoTestReport(type: JacocoReport, description: 'Generates aggregated coverage report') {
    dependsOn jacocoMerge
    executionData jacocoMerge.destinationFile
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    reports.xml.enabled = true
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
}

htmlDependencyReport.projects = allprojects

