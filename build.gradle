plugins {
    id 'ru.vyarus.java-lib' version '2.1.0' apply false
    id 'ru.vyarus.github-info' version '1.2.0' apply false
    id 'ru.vyarus.quality' version '4.4.0' apply false
    id 'com.jfrog.bintray' version '1.8.5' apply false
    id "io.spring.dependency-management" version "1.0.10.RELEASE" apply false

    id 'base'
    id 'jacoco'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.34.0'
}

wrapper {
    gradleVersion = '6.3'
}

description = 'Dropwizard-guicey extensions'

allprojects {
    apply plugin: 'project-report'
    apply plugin: 'com.jfrog.bintray'

    repositories { jcenter(); mavenCentral(); mavenLocal(); maven { url 'https://jitpack.io' } }

    bintray {
        user = findProperty('bintrayUser')
        key = findProperty('bintrayKey')
        pkg {
            repo = 'xvik'
            name = rootProject.name
            desc = rootProject.description
            labels = ['dropwizard', 'guice']
            publicDownloadNumbers = true
        }
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'ru.vyarus.java-lib'
    // explicit spotbugs required for fixing classpath after dependencies plugin (remove later)
    apply plugin: 'ru.vyarus.quality'
    apply plugin: ru.vyarus.gradle.plugin.quality.spotbugs.CustomSpotBugsPlugin
    apply plugin: 'ru.vyarus.github-info'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    ext {
        guicey = '5.1.0'
        guiceExtAnn = '1.3.0'
    }

    dependencyManagement {
        // force guicey snapshot updates
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
        imports {
            mavenBom "ru.vyarus:dropwizard-guicey:$guicey"
        }
        dependencies {
            dependency 'io.dropwizard.modules:dropwizard-jdbi:2.0.14'
            dependency "ru.vyarus:guice-ext-annotations:$guiceExtAnn"
            dependency 'ru.vyarus:guice-validator:2.0.0'
            dependency 'com.h2database:h2:1.4.200'
            dependency 'io.dropwizard.modules:dropwizard-flyway:2.0.14-2'
        }
    }
    dependencies {
        provided 'com.google.code.findbugs:jsr305:3.0.2'
        provided 'com.google.code.findbugs:annotations:3.0.1'

        implementation 'ru.vyarus:dropwizard-guicey'

        testImplementation 'org.spockframework:spock-core'
        testImplementation 'io.dropwizard:dropwizard-testing'
        testImplementation 'com.github.stefanbirkner:system-rules'

        // dependency management plugin affects spotbugs configuration lowering asm to 7.2 (due to guice bom)
        spotbugs "com.github.spotbugs:spotbugs:${quality.spotbugsVersion}"
        spotbugs 'org.ow2.asm:asm:8.0.1'
    }

    group = 'ru.vyarus.guicey'

    github {
        user = 'xvik'
        repository = rootProject.name
        license = 'MIT'
        licenseUrl = github.rawFileUrl('LICENSE');
        changelogFile = 'CHANGELOG.md'
        vcsUrl = 'https://github.com/xvik/dropwizard-guicey-ext'
    }

    pom {
        developers {
            developer {
                id 'xvik'
                name 'Vyacheslav Rusakov'
                email 'vyarus@gmail.com'
            }
        }
    }

    jar {
        manifest {
            attributes 'Automatic-Module-Name': project.name.replace('guicey-', 'ru.vyarus.dropwizard.guicey.')
        }
    }

    bintray {
        publications = ['maven']
    }

    test {
        testLogging {
            events 'skipped', 'failed'
            exceptionFormat 'full'
        }
        maxHeapSize = '512m'
    }
    jacocoTestReport.reports.xml.enabled = true

    dependencyUpdates.revision = 'release'

    // disable gradle metadata publishing (because it confuse A LOT)
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
            'Generates dependencies tree report and opens it in browser') {
        doLast {
            java.awt.Desktop.getDesktop().open(file("build/reports/project/dependencies/root.${project.name}.html"))
        }
    }
}

/*
 Each module published separately: subprojects simply upload files to bintray and root project will
 publish/generate signatures/sync with maven central.
 Note: plugin will find bintray tasks in submodules and depend on it so order will be always correct.
 */
bintray {
    publish = true
    pkg {
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

// by default, task depends on subproject tasks directly, which breaks dependencies graph
bintrayUpload.dependsOn subprojects.collect { ":$it.name:bintrayUpload" }

// dependency on all subprojects required for release validation
check.dependsOn subprojects.check

// release manages only root project (subprojects will be checked and released implicitly)
afterReleaseBuild {
    dependsOn = ['bintrayUpload']
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

task testReport(type: TestReport, description: 'Generates an aggregate test report') {
    destinationDir = file("${buildDir}/reports/tests")
    reportOn subprojects.test
}

task jacocoMerge(type: JacocoMerge, description: 'Merge coverage results for aggregated report generation') {
    executionData = files(subprojects.collect {it.file('build/jacoco/test.exec')})
    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}

task jacocoTestReport(type: JacocoReport, description: 'Generates aggregated coverage report') {
    dependsOn jacocoMerge
    executionData jacocoMerge.destinationFile
    sourceDirectories.setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    classDirectories.setFrom(files(subprojects.sourceSets.main.output))
    reports.xml.enabled = true
}

htmlDependencyReport.projects = allprojects
